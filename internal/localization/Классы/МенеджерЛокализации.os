#Область ОписаниеПеременных

Перем СловарьЛокализации;
Перем Рефлектор;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ЛокализованныеСтроки(МодульЛокализации) Экспорт
	
	ЛокализованныеСтроки = СловарьЛокализации.Получить(МодульЛокализации);
	
	Если ЛокализованныеСтроки = Неопределено Тогда

		Если Рефлектор.МетодСуществует(МодульЛокализации, "ЛокализованныеСтроки") Тогда
			
			ЛокализованныеСтроки = МодульЛокализации.ЛокализованныеСтроки();
		
		Иначе

			ЛокализованныеСтроки = ЛокализованныеСтрокиПоАннотации(МодульЛокализации);
		
		КонецЕсли;

		СловарьЛокализации.Вставить(МодульЛокализации, ЛокализованныеСтроки);

	КонецЕсли;
	
	Возврат ЛокализованныеСтроки;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта()
	
	СловарьЛокализации = Новый Соответствие;
	Рефлектор = Новый Рефлектор;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЛокализованныеСтрокиПоАннотации(МодульЛокализации) 

	ЛокализованныеСтроки = Новый Структура;
	ИмяАннотации = "локализованнаястрока";
	
	МетодыОбъекта = Рефлектор.ПолучитьТаблицуМетодов(МодульЛокализации);

	Для каждого МетодОбъекта Из МетодыОбъекта Цикл
		
		Если МетодОбъекта.ЭтоФункция
			И МетодОбъекта.Экспорт
			И ЕстьАннотация(МетодОбъекта, ИмяАннотации) Тогда

			ЛокализованнаяСтрока = Рефлектор.ВызватьМетод(МодульЛокализации, МетодОбъекта.Имя);
			ЛокализованныеСтроки.Вставить(МетодОбъекта.Имя, ЛокализованнаяСтрока);

		КонецЕсли;	

	КонецЦикла;

	Возврат ЛокализованныеСтроки;

КонецФункции

Функция ЕстьАннотация(МетодОбъекта, ИмяАннотации)
	
	ЕстьАннотация = Ложь;
	
	Для каждого Аннотация Из МетодОбъекта.Аннотации Цикл
			
		Если НРег(Аннотация.Имя) = ИмяАннотации Тогда
			ЕстьАннотация = Истина;
			Прервать;		
		КонецЕсли;

	КонецЦикла;

	Возврат ЕстьАннотация;

КонецФункции

#КонецОбласти